buildscript {
  repositories {
    jcenter()
    maven { url = 'http://maven.fabricmc.net' }
  }
  dependencies {
    classpath 'net.fabricmc:fabric-loom:0.1.0-SNAPSHOT'
  }
}

plugins {
    id "com.jfrog.artifactory" version "4.7.5"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = "net.voxelindustry"
    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"

	if (project != rootProject) {
  		eclipse.project.name = (rootProject.name + project.path).replaceAll(':', '-')
	}

    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = 'voxelindustry'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
            }
        }
    }

	// Fix JDK version use in eclipse
	eclipse {
		jdt {
			sourceCompatibility = 1.8
			targetCompatibility = 1.8
		}
	}

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        maven { url 'http://maven.ferenyr.info/artifactory/libs-release'}
        mavenCentral()
    }

    jar {
        duplicatesStrategy = 'exclude'
        exclude('net/voxelindustry/brokkgui/demo/**')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifactId archivesBaseName
                artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
            }
        }
    }

	// Add references to core project
	tasks.cleanEclipse.doLast {
		project.configurations.stream()
		.flatMap({config -> config.dependencies.stream()})                  // get all deps
		.filter({dep -> dep instanceof ProjectDependency})                  // that are Projects
		.forEach({dep -> eclipse.project.referencedProjects.add(dep.name)}) // and add to referencedProjects
	}
	tasks.eclipse.dependsOn(cleanEclipse)

    artifactoryPublish.dependsOn build
}

project(":core") {
    archivesBaseName = "brokkgui-core"

    version = "1.0.0-32-SNAPSHOT"
    dependencies {
        api "fr.ourten:teabeans:0.7.0"
        api "net.voxelindustry:hermod:1.0.0"
        implementation "org.apache.commons:commons-lang3:3.4"
        implementation "com.google.code.findbugs:jsr305:3.0.1"
        implementation "commons-io:commons-io:2.5"
        implementation "com.google.guava:guava:21.0"

        testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.8.47'
    }
}

project(":fabric-1.14") {
    apply plugin: 'fabric-loom'

    archivesBaseName = "brokkgui-binding-fabric1.14"

    version = "1.0.0-32-SNAPSHOT"

    dependencies {
        api project(":core")

        minecraft "com.mojang:minecraft:18w50a"
        mappings "net.fabricmc:yarn:18w50a.+"
        modCompile "net.fabricmc:fabric-loader:0.2.0.70"
        modCompile "net.fabricmc:fabric:0.1.1.51"
    }
}

project(":1.11") {
    archivesBaseName = "brokkgui-binding-mc1.11"

    version = "1.0.0-32-SNAPSHOT"
    dependencies {
        api project(":core")
    }
}

project(":1.12") {
    archivesBaseName = "brokkgui-binding-mc1.12"

    version = "1.0.0-32-SNAPSHOT"
    dependencies {
        api project(":core")
    }
}