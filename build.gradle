plugins {
    id "com.jfrog.artifactory" version "4.7.5"
    id "org.sonarqube" version "2.7"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = "net.voxelindustry"
    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"

	if (project != rootProject) {
  		eclipse.project.name = (rootProject.name + project.path).replaceAll(':', '-')
	}

    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = 'voxelindustry'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
            }
        }
    }

	// Fix JDK version use in eclipse
	eclipse {
		jdt {
			sourceCompatibility = 1.8
			targetCompatibility = 1.8
		}
	}

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        maven { url 'http://maven.ferenyr.info/artifactory/libs-release'}
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifactId archivesBaseName
                artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
            }
        }
    }

	// Add references to core project
	tasks.cleanEclipse.doLast {
		project.configurations.stream()
		.flatMap({config -> config.dependencies.stream()})                  // get all deps
		.filter({dep -> dep instanceof ProjectDependency})                  // that are Projects
		.forEach({dep -> eclipse.project.referencedProjects.add(dep.name)}) // and add to referencedProjects
	}
	tasks.eclipse.dependsOn(cleanEclipse)

    artifactoryPublish.dependsOn build
}

project(":common") {
    archivesBaseName = "brokkgui-common"

    version = project.brokkgui_version
    dependencies {
        api project(":style")

        api "fr.ourten:teabeans:0.7.0"
        api "net.voxelindustry:hermod:1.0.0"
        implementation "org.apache.commons:commons-lang3:3.4"
        implementation "com.google.code.findbugs:jsr305:3.0.1"
        implementation "commons-io:commons-io:2.5"
        implementation "com.google.guava:guava:21.0"

        testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.8.47'
    }
}

project(":element") {
    archivesBaseName = "brokkgui-elements"

    version = project.brokkgui_version

    dependencies {
        api project(":common")

        implementation "org.apache.commons:commons-lang3:3.4"
        implementation "com.google.code.findbugs:jsr305:3.0.1"
        implementation "commons-io:commons-io:2.5"
        implementation "com.google.guava:guava:21.0"
    }
}

project(":style") {
    archivesBaseName = "brokkgui-style"

    version = project.brokkgui_version

    dependencies {
        api "fr.ourten:teabeans:0.7.0"

        implementation "org.apache.commons:commons-lang3:3.4"
        implementation "com.google.code.findbugs:jsr305:3.0.1"
        implementation "commons-io:commons-io:2.5"
        implementation "com.google.guava:guava:21.0"
    }
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property "sonar.host.url", project.sonarqube_url
        property "sonar.login", project.sonarqube_login
        property "sonar.projectKey", "BrokkGUI"
    }
}