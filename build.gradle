plugins {
    id "com.jfrog.artifactory" version "4.7.5"
    id "org.sonarqube" version "2.7"
    id "jacoco"
}

project.ext {
    commonsLangVersion = "3.4"
    commonsIoVersion = "2.5"
    guavaVersion = "21.0"
    findbugsJsr305Version = "3.0.1"
    teabeansVersion = "1.1.0"
    hermodVersion = "2.0.0"
    junitVersion = "5.3.2"
    assertjVersion = "3.10.0"
    mockitoVersion ="2.24.0"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = "net.voxelindustry"
    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"

    if (project != rootProject) {
        eclipse.project.name = (rootProject.name + project.path).replaceAll(':', '-')
    }

    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = 'voxelindustry'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications('mavenJava')
            }
        }
    }

    // Fix JDK version use in eclipse
    eclipse {
        jdt {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    repositories {
        maven { url 'http://maven.ferenyr.info/artifactory/libs-release' }
        mavenCentral()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifactId archivesBaseName
                artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
            }
        }
    }

    // Add references to core project
    tasks.cleanEclipse.doLast {
        project.configurations.stream()
                .flatMap({ config -> config.dependencies.stream() })                  // get all deps
                .filter({ dep -> dep instanceof ProjectDependency })                  // that are Projects
                .forEach({ dep -> eclipse.project.referencedProjects.add(dep.name) }) // and add to referencedProjects
    }
    tasks.eclipse.dependsOn(cleanEclipse)

    artifactoryPublish.dependsOn build

    apply plugin: 'jacoco'

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test.finalizedBy jacocoTestReport
}

project(":common") {
    archivesBaseName = "brokkgui-common"

    version = project.brokkgui_version
    dependencies {
        api "fr.ourten:teabeans:${teabeansVersion}"
        api "net.voxelindustry:hermod:${hermodVersion}"
        implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
        implementation "com.google.code.findbugs:jsr305:${findbugsJsr305Version}"
        implementation "commons-io:commons-io:${commonsIoVersion}"
        implementation "com.google.guava:guava:${guavaVersion}"

        testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}" 
        testCompile "org.mockito:mockito-junit-jupiter:${mockitoVersion}" 
        testCompile "org.assertj:assertj-core:${assertjVersion}" 

        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }
}

project(":style") {
    archivesBaseName = "brokkgui-style"

    version = project.brokkgui_version

    dependencies {
        api project(":common")

        implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
        implementation "com.google.code.findbugs:jsr305:${findbugsJsr305Version}"
        implementation "commons-io:commons-io:${commonsIoVersion}"
        implementation "com.google.guava:guava:${guavaVersion}"

        testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}" 
        testCompile "org.mockito:mockito-junit-jupiter:${mockitoVersion}" 
        testCompile "org.assertj:assertj-core:${assertjVersion}" 

        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }
}

project(":element") {
    archivesBaseName = "brokkgui-elements"

    version = project.brokkgui_version

    dependencies {
        api project(":common")
        api project(":style")

        implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
        implementation "com.google.code.findbugs:jsr305:${findbugsJsr305Version}"
        implementation "commons-io:commons-io:${commonsIoVersion}"
        implementation "com.google.guava:guava:${guavaVersion}"

        testCompile "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}" 
        testCompile "org.mockito:mockito-junit-jupiter:${mockitoVersion}" 
        testCompile "org.assertj:assertj-core:${assertjVersion}" 

        testRuntime "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    }
}

project(":core") {
    archivesBaseName = "brokkgui-core"

    version = project.brokkgui_version

    dependencies {
        api project(":common")
        api project(":element")

        api "fr.ourten:teabeans:${teabeansVersion}"

        implementation "org.apache.commons:commons-lang3:${commonsLangVersion}"
        implementation "com.google.code.findbugs:jsr305:${findbugsJsr305Version}"
        implementation "commons-io:commons-io:${commonsIoVersion}"
        implementation "com.google.guava:guava:${guavaVersion}"
    }
}

apply plugin: 'org.sonarqube'

sonarqube {
    properties {
        property "sonar.host.url", project.hasProperty('sonarqube_url') ? project.sonarqube_url : System.getenv('SONARQUBE_URL')
        property "sonar.login", project.hasProperty('sonarqube_login') ? project.sonarqube_login : System.getenv('SONARQUBE_LOGIN')
        property "sonar.projectKey", "net.voxelindustry:BrokkGUI"
        property "sonar.projectName", "BrokkGUI"
        property "sonar.groovy.binaries", "${project.buildDir.name}${File.separator}classes"
    }
}

apply plugin: 'java'
apply plugin: 'jacoco'

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test.finalizedBy jacocoTestReport
project.tasks["sonarqube"].dependsOn jacocoTestReport